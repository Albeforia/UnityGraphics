// We need only need 1 bounce for RTAS Debug
#pragma max_recursion_depth 1

// Raise the debug display flag
#define RTAS_DEBUG_DISPLAY

// HDRP include
#define SHADER_TARGET 50
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Macros.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Packing.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariablesFunctions.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/ShaderVariablesRaytracing.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/RenderPipeline/Raytracing/Shaders/RaytracingIntersection.hlsl"

// Output structure of the reflection raytrace shader
int _LayerMask;
int _DebugMode;
float4x4 _PixelCoordToViewDirWS;
RW_TEXTURE2D_X(float4, _OutputDebugBuffer);

[shader("miss")]
void MissShaderDebug(inout RayIntersectionVisiblity rayIntersection : SV_RayPayload)
{
    rayIntersection.t = FLT_INF;
    rayIntersection.barycentrics = float2(0.0, 0.0);
}

float3 IntToColor(uint val)
{
    int r = (val & 0xFF0000) >> 16;
    int g = (val & 0x00FF00) >> 8;
    int b = val & 0x0000FF;
    return float3(r / 255.0, g / 255.0, b / 255.0);
}

[shader("raygeneration")]
void RTASDebug()
{
    uint3 LaunchIndex = DispatchRaysIndex();

    UNITY_XR_ASSIGN_VIEW_INDEX(LaunchIndex.z);

    // Pixel coordinate of the current pixel
    uint2 currentPixelCoord = uint2(LaunchIndex.x, LaunchIndex.y);

    // Create the ray descriptor for this pixel
    RayDesc rayDescriptor;
    rayDescriptor.TMin = 0.01;
    rayDescriptor.TMax = FLT_INF;
    rayDescriptor.Origin = GetPrimaryCameraPosition();
    rayDescriptor.Direction = -normalize(mul(float4(currentPixelCoord, 1.0, 1.0), _PixelCoordToViewDirWS).xyz);

    // Create and init the RayIntersection structure for this
    RayIntersectionVisiblity rayIntersection;
    rayIntersection.color = float3(0.0, 0.0, 0.0);
    rayIntersection.t = 0.0f;
    rayIntersection.pixelCoord = currentPixelCoord;
    rayIntersection.barycentrics = float2(0.0, 0.0);

    // Evaluate the ray intersection
    TraceRay(_RaytracingAccelerationStructure, RAY_FLAG_CULL_BACK_FACING_TRIANGLES, _LayerMask, 0, 1, 0, rayDescriptor, rayIntersection);

    float3 debugColor = 0.0;
    if (_DebugMode == 0)
    {
        debugColor = rayIntersection.t != FLT_INF ? IntToColor(JenkinsHash(rayIntersection.instanceIndex + 1)): 0.0;
    }
    else 
    {
        debugColor = rayIntersection.t != FLT_INF ? IntToColor(JenkinsHash(rayIntersection.primitiveIndex)): 0.0;
    }

    // Alright we are done
    _OutputDebugBuffer[COORD_TEXTURE2D_X(currentPixelCoord)] = float4(debugColor, 1.0);
}