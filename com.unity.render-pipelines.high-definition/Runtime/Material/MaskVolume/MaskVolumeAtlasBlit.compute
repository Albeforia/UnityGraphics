#pragma kernel MaskVolumeAtlasBlitKernel MASK_VOLUME_ATLAS_BLIT_KERNEL=MaskVolumeAtlasBlitKernel

#ifdef SHADER_API_PSSL
#   pragma argument( scheduler=minpressure ) // instruct the shader compiler to prefer minimizing vgpr usage
#endif

#pragma only_renderers d3d11 playstation xboxone vulkan metal switch

#include "Packages/com.unity.render-pipelines.high-definition-config/Runtime/ShaderConfig.cs.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/Material/MaskVolume/MaskVolumeRendering.cs.hlsl"

StructuredBuffer<int> _MaskVolumeAtlasReadSHL0Buffer;
RWTexture3D<float4> _MaskVolumeAtlasWriteTextureSH;

uint _MaskVolumeAtlasReadBufferCount;
float3 _MaskVolumeResolution;
float3 _MaskVolumeResolutionInverse;
float3 _MaskVolumeAtlasScale;
float3 _MaskVolumeAtlasBias;
float4 _MaskVolumeAtlasResolutionAndSliceCount;
float4 _MaskVolumeAtlasResolutionAndSliceCountInverse;

uint3 ComputeWriteIndexFromReadIndex(uint readIndex, float3 resolution)
{
    // _MaskVolumeAtlasReadBuffer[z * resolutionY * resolutionX + y * resolutionX + x]
    // TODO: Could implement as floating point operations, which is likely faster.
    // Would need to verify precision.
    uint x = readIndex % (uint)resolution.x;
    uint y = (readIndex / (uint)resolution.x) % (uint)resolution.y;
    uint z = readIndex / ((uint)resolution.y * (uint)resolution.x);

    return uint3(x, y, z);
}

float UnpackUNormByte(int value, int byteIndex)
{
    return (value >> byteIndex * 8 & 0xFF) / 255.0;
}

// Warning this needs to match with kBatchSize in MaskVolumeRendering.cs
#define BATCH_SIZE 256
[numthreads(BATCH_SIZE, 1, 1)]
void MASK_VOLUME_ATLAS_BLIT_KERNEL(uint groupThreadId : SV_GroupThreadID, uint groupId : SV_GroupID, uint3 dispatchThreadId : SV_DispatchThreadID)
{
    uint readIndex = groupId * BATCH_SIZE + groupThreadId;
    if (readIndex >= _MaskVolumeAtlasReadBufferCount) { return; }

    uint3 writeIndex = ComputeWriteIndexFromReadIndex(
        readIndex,
        _MaskVolumeResolution
    );

    writeIndex += (uint3)floor(_MaskVolumeAtlasBias * _MaskVolumeAtlasResolutionAndSliceCount.xyz);

    const uint SH_STRIDE_L0 = 1;
    
    _MaskVolumeAtlasWriteTextureSH[uint3(writeIndex.x, writeIndex.y, writeIndex.z + _MaskVolumeAtlasResolutionAndSliceCount.z * 0)] = float4(
        UnpackUNormByte(_MaskVolumeAtlasReadSHL0Buffer[readIndex * SH_STRIDE_L0], 0),
        UnpackUNormByte(_MaskVolumeAtlasReadSHL0Buffer[readIndex * SH_STRIDE_L0], 1),
        UnpackUNormByte(_MaskVolumeAtlasReadSHL0Buffer[readIndex * SH_STRIDE_L0], 2),
        0.0f
    );
}
